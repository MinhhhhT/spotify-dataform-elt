config {
    type: "incremental", 
    schema: "dataform_staging",
    name: "stg_track",
    uniqueKey: ["track_nk"],
    description: "Table stores track's data"
}

SELECT DISTINCT
    RIGHT(s.track_uri, 22) AS track_nk,
    s.track_uri, 
    s.track_name, 
    s.album_name, 

    ${when(incremental(),
        `COALESCE(t.load_time, ${utils.get_current_datetime()})`,
        `${utils.get_current_datetime()}`
    )} AS load_time,

    ${utils.get_current_datetime()} AS updated_at,
    '${dataform.projectConfig.vars.source_name}' AS source_system
FROM ${ref("stg_spotify_track_reference")} AS s

${when(incremental(),
    `LEFT JOIN ${self()} AS t
        ON t.track_uri = s.track_uri`
)}

WHERE s.track_uri IS NOT NULL
